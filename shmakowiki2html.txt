ometa ShmakoWikiToHtml {
    keyword = 'para'
            | 'header1' | 'header2' | 'header3'
            | 'olist' | 'ulist' | 'olistItem' | 'ulistItem'
            | 'bold' | 'bold_'
            | 'italic' | 'italic_'
            | 'underline' | 'underline_'
            | 'strike' | 'strike_'
            | 'monospace' | 'monospace_'
            | 'superscript' | 'superscript_'
            | 'subscript' | 'subscript_'
            | 'link' | 'link_'
            | 'lineBreak'
            | 'escaped'
            | 'extention',

    token = [keyword:t apply(t):ans] -> ans
          | :c -> ShmakoWikiToHtml._escape(c),
    tokens = [token*:c] -> c.join(''),


    bold tokens:c -> ('<b>' + c + '</b>'),
    bold_ tokens:c -> ('<b>' + c + '</b>'),

    italic tokens:c -> ('<i>' + c + '</i>'),
    italic_ tokens:c -> ('<i>' + c + '</i>'),

    underline tokens:c -> ('<u>' + c + '</u>'),
    underline_ tokens:c -> ('<u>' + c + '</u>'),

    strike tokens:c -> ('<s>' + c + '</s>'),
    strike_ tokens:c -> ('<s>' + c + '</s>'),

    monospace tokens:c -> ('<tt>' + c + '</tt>'),
    monospace_ tokens:c -> ('<tt>' + c + '</tt>'),

    superscript tokens:c -> ('<sup>' + c + '</sup>'),
    superscript_ tokens:c -> ('<sup>' + c + '</sup>'),

    subscript tokens:c -> ('<sub>' + c + '</sub>'),
    subscript_ tokens:c -> ('<sub>' + c + '</sub>'),

    link token:c [] -> ('<a href="' + c + '">' + c + '</a>'),
    link token:c tokens:cc -> ('<a href="' + c + '">' + cc + '</a>'),
    link_ token:c -> ('<a href="' + c + '">' + c + '</a>'),

    lineBreak :c -> ('<br/>'),

    escaped tokens:c -> ('<span>' + c + '</span>'),

    para tokens:c -> ('<p>' + c + '</p>'),

    header1 tokens:c -> ('<h1>' + c + '</h1>'),
    header2 tokens:c -> ('<h2>' + c + '</h2>'),
    header3 tokens:c -> ('<h3>' + c + '</h3>'),

    olist tokens:c -> ('<ol>' + c + '</ol>'),
    ulist tokens:c -> ('<ul>' + c + '</ul>'),
    olistItem tokens:c -> ('<li>' + c + '</li>'),
    ulistItem tokens:c -> ('<li>' + c + '</li>'),

    extention :t :c :p -> (ShmakoWikiToHtml.extentions.hasOwnProperty(t) ?
                           ShmakoWikiToHtml.extentions[t](c, p) :
                           '<div>' + ShmakoWikiToHtml._escape(c) + '</div>'),

    topLevel = tokens
}

ShmakoWikiToHtml.extentions = {
    'html': function(c) { return c },
    'hljs': function(c, p) {
            return '<pre><code class="'+ p + '">' +
                    ShmakoWikiToHtml._escape(c) +
                '</code></pre>'
        }
};

ShmakoWikiToHtml._escape = (function() {
    var amp = new RegExp('&', 'g'),
        lt = new RegExp('<', 'g'),
        gt = new RegExp('>', 'g'),
        apos = new RegExp("'", 'g'),
        quot = new RegExp('"', 'g');

    return function(s) {
        return String(s)
            .replace(amp, '&amp;')
            .replace(lt, '&lt;')
            .replace(gt, '&gt;')
            .replace(apos, '&apos;')
            .replace(quot, '&quot;');
    }
})()
