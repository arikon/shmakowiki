ometa W {
    oneOf :a ?(a.length == 1) = apply(a.pop()),
    oneOf :a ?(a.length > 1) = apply(a.pop()) | oneOf(a),

    escapedChar = '~' char:c -> c,
    escaped = escapedChar+:c -> [`escaped, c.join('')],

    b = seq('**'),
    i = seq('//'),
    s = seq('--'),
    special = (b | i | s):c -> [`special, c],

    between :t :n = (apply(t) -> W.arrAdd(n, t)):nn (~oneOf(W.arrCopy(nn)) allInline(nn))*:c apply(t) -> c,
    between_ :t = apply(t) (~special text)*:c (~~special | end) -> c,

    bold :n = between(`b, n):c -> [`bold, c],
    bold_ = between_(`b):c -> [`bold_, c],

    italic :n = between(`i, n):c -> [`italic, c],
    italic_ = between_(`i):c -> [`italic_, c],

    strike :n = between(`s, n):c -> [`strike, c],
    strike_ = between_(`s):c -> [`strike_, c],

    text = (~special ~escapedChar char)+:c -> c.join('')
           | escaped:c -> c,

    inline :n = bold(n) | italic(n) | strike(n) | text,
    inline_ = bold_ | italic_ | strike_,

    allInline :n = inline(n) | (~inline(n) inline_),
    topInline = allInline([])+,


    spacesNoNl   = (~'\n' space)*,

    nl = '\n' (spacesNoNl '\n')+,

    uli = spacesNoNl:s '*' ~'*' spacesNoNl -> s.join(''),
    oli = spacesNoNl:s digit+ '.' spacesNoNl -> s.join(''),

    anyLi = uli | oli,
    anySubLi :ss = anyLi:s ?(s.length > ss.length) -> s,

    blockEnd = nl | ('\n' ~~anyLi) | end,

    para = ~anyLi ((~blockEnd char)+:cc -> cc.join('')):c  blockEnd -> [`para, W.matchAll(c, 'topInline')],

    listItem :t :l = apply(t):s ?(s.length >= l.length)
                     ((~blockEnd char)+:cc -> W.matchAll(cc.join(''), 'topInline')):c blockEnd
                     (~~(anySubLi(s):ss) anyList(ss))*:ll -> { if (ll) c = W.arrJoin(c, ll); c },

    ulistItem :l = listItem(`uli, l):c -> [`ulistItem, c],
    olistItem :l = listItem(`oli, l):c -> [`olistItem, c],

    ulist :l = ulistItem(l)+:c -> [`ulist, c],
    olist :l = olistItem(l)+:c -> [`olist, c],

    anyList :l = ulist(l) | olist(l),

    allBlock = para | anyList(''),

    topLevel = allBlock+
}

W.arrJoin = function(arr1, arr2) {
    var newArr = W.arrCopy(arr1);
    for (var i = 0; i < arr2.length; i++) {
       newArr[newArr.length] = arr2[i];
    }
    return newArr;
}

W.arrCopy = function(arr) {
    var newArr = [];
    for (var i = 0; i < arr.length; i++) {
       newArr[newArr.length] = arr[i];
    }
    return newArr;
}

W.arrAdd = function(arr, elem) {
    for (var i = 0; i < arr.length; i++) {
       if (arr[i] == elem) return arr;
    }
    arr[arr.length] = elem;
    return arr;
}


